plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'org.panteleyev.jpackageplugin' version '1.7.6'
}

group = 'org.truetranslation'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.45.1.0'
    implementation 'info.picocli:picocli:4.7.7'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.7'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.slf4j:slf4j-nop:1.7.32'
    implementation 'com.formdev:flatlaf:3.2.5'
    implementation 'com.formdev:flatlaf-intellij-themes:3.2.5'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    
    options.compilerArgs += [
        "-Aproject=${project.group}/${project.name}",
    ]
}

application {
    mainClass = 'org.truetranslation.mybible.cli.Main'
    applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8", "-Dsun.stdout.encoding=UTF-8"]
}

shadowJar {
    archiveBaseName = 'mybible-cli'
    archiveClassifier = ''
    archiveVersion = ''
    mergeServiceFiles()
}

jpackage {
    dependsOn 'shadowJar'
    
    appName = 'mybible-cli'
    appVersion = '1.0'
    vendor = 'Kos Ivantsov'
    copyright = 'Copyright Â© 2025 Kos Ivantsov'
    appDescription = 'MyBible CLI - A command-line and GUI tool for biblical text processing'
    
    input = file("${buildDir}/libs")
    mainJar = 'mybible-cli.jar'
    mainClass = application.mainClass.get()
    
    destination = layout.buildDirectory.dir('jpackage')
    
    javaOptions = [
        '-Dfile.encoding=UTF-8',
        '-Dsun.stdout.encoding=UTF-8'
    ]
    
    mac {
        macPackageIdentifier = 'org.truetranslation.mybible-cli'
        macPackageName = 'mybible-cli'
        type = 'APP_IMAGE'
        icon = file('src/main/resources/icons/mybible-cli.icns')
        macAppCategory = 'public.app-category.utilities'
        javaOptions = [
            '-Dfile.encoding=UTF-8',
            '-Dsun.stdout.encoding=UTF-8',
            '-Dapple.awt.application.appearance=system',
            '-Dapple.laf.useScreenMenuBar=true',
            '-Dapple.awt.application.name=mybible-cli',
            '-Xdock:name=mybible-cli'
        ]
    }
    
    windows {
        winMenu = true
        winDirChooser = true
        winShortcut = true
        winPerUserInstall = false
        type = 'EXE'
        icon = file('src/main/resources/icons/mybible-cli.ico')
    }
    
    linux {
        linuxPackageName = 'mybible-cli'
        linuxDebMaintainer = 'kos@truetranslation.org'
        linuxAppCategory = 'utils'
        linuxShortcut = true
        type = 'DEB'
        icon = file('src/main/resources/icons/mybible-cli.png')
    }
}
